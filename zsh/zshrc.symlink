DEFAULT_USER="Adam"

# Shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# Your project folder that we can `c [tab]` to
export PROJECTS=~/Dev

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# Source all zsh files in DOTFILES
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Oh My Zsh
ZSH=$HOME/.oh-my-zsh
ZSH_THEME="agnoster"
plugins=(git history history-substring-search terminalapp brew zsh-syntax-highlighting)
source $ZSH/oh-my-zsh.sh

# Initialize autocomplete here, otherwise functions won't be loaded
# autoload -U compinit
# compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
